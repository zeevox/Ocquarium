/*
 * Copyright (C) 2017 The Android Open Source Project
 * Copyright (C) 2020 Timothy "ZeevoX" Langer
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

static def buildDate() {
    def df = new SimpleDateFormat("yyyyMMdd")
    return df.format(new Date())
}

def appVersionCode = Integer.valueOf(System.getenv("TRAVIS_BUILD_NUMBER") ?: 0)

android {
    compileSdkVersion 34

    signingConfigs {
        travis {
            storeFile file("../ocquarium_keystore.jks")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    defaultConfig {
        applicationId "com.zeevox.octo"
        minSdkVersion 21
        targetSdkVersion 34

        if (System.getenv("TRAVIS") == "true") {
            versionCode appVersionCode
            versionName = "Alpha build #" + appVersionCode
            android.applicationVariants.configureEach { variant ->
                variant.outputs.configureEach {
                    outputFileName = "Ocquarium-" + appVersionCode + "-alpha.apk"
                }
            }
        } else {
            versionCode 22
            versionName "3.5.0"
            android.applicationVariants.configureEach { variant ->
                variant.outputs.configureEach {
                    outputFileName = "Ocquarium-${versionName}-${buildDate()}-release.apk"
                }
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            applicationIdSuffix '.debug'
            versionNameSuffix ' debug'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
        }
        travis {
            debuggable true
            applicationIdSuffix '.alpha'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
            if (System.getenv("TRAVIS") == "true") {
                signingConfig signingConfigs.travis
            }
        }
    }
    namespace 'com.zeevox.octo'
}

dependencies {
    implementation 'androidx.annotation:annotation:1.8.0'
    implementation 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'com.github.martin-stone:hsv-alpha-color-picker-android:3.1.0'
    implementation 'androidx.preference:preference:1.2.1'
}
